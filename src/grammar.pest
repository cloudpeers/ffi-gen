ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

primitive = {
    "u8" | "u16" | "u32" | "u64" | "usize" |
    "i8" | "i16" | "i32" | "i64" | "isize" |
    "bool" | "f32" | "f64" | "string"
}
ref_ = { "&" ~ type_ }
slice = { "[" ~ type_ ~ "]" }
vec = { "Vec" ~ "<" ~ type_ ~ ">" }
opt = { "Option" ~ "<" ~ type_ ~ ">" }
res = { "Result" ~ "<" ~ type_ ~ ">" }
fut = { "Future" ~ "<" ~ type_ ~ ">" }
stream = { "Stream" ~ "<" ~ type_ ~ ">" }
type_ = { primitive | ref_ | slice | vec | opt | res | fut | stream | ident }

doc = @{ (!"\n" ~ ANY)* ~ "\n" }
static_ = { "static" }
arg = { ident ~ ":" ~ type_ }
args = { (arg ~ ("," ~ arg)*)? }
function = { ("///" ~ doc)* ~ static_? ~ "fn" ~ ident ~ "(" ~ args ~ ")" ~ ("->" ~ type_)?  ~ ";" }
object = { ("///" ~ doc)* ~ "object" ~ ident ~ "{" ~ function* ~ "}" }

root = { SOI ~ ("//!" ~ doc)* ~ (object | function)* ~ EOI }

WHITESPACE = _{ " " | "\t" | "\n" }
COMMENT = _{ ("/*" ~ (!"*/" ~ ANY)* ~ "*/") | ("//" ~ !("/" | "!" | "\n") ~ (!"\n" ~ ANY)* ~ "\n") }
